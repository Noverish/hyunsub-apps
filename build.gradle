import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

import java.text.SimpleDateFormat

plugins {
    id 'org.springframework.boot' version '3.0.4'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'org.jetbrains.kotlin.kapt' version '1.8.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.8.22'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.8.22'
    id 'org.jlleitschuh.gradle.ktlint' version '11.5.0'
}

var javaVersion = JavaVersion.VERSION_17

allprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.kapt'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
    apply plugin: 'org.jlleitschuh.gradle.ktlint'

    group = 'kim.hyunsub'
    version = new SimpleDateFormat("yyyyMMdd_HHmmss").format(new Date())
    sourceCompatibility = javaVersion

    configurations {
        compileOnly {
            extendsFrom kapt
        }
    }

    kapt {
        keepJavacAnnotationProcessors = true
        useBuildCache = false
        includeCompileClasspath = false
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = javaVersion
            freeCompilerArgs = ['-Xjsr305=strict']
        }
    }

    ktlint {
        disabledRules = ["trailing-comma-on-call-site"]
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-websocket'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'
        kapt 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        implementation 'io.github.openfeign:feign-jackson:12.2'
        implementation 'io.github.openfeign:feign-okhttp'

        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2'
        implementation 'io.github.microutils:kotlin-logging-jvm:3.0.5'

        runtimeOnly 'mysql:mysql-connector-java:8.0.33'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.5.5'
        testImplementation 'io.kotest:kotest-assertions-core-jvm:5.5.5'
        testImplementation 'io.kotest.extensions:kotest-extensions-spring:1.1.2'
        testImplementation 'com.ninja-squad:springmockk:3.1.2'
        testImplementation 'io.mockk:mockk:1.13.4'
        testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-starter:0.4.7'
        testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-kotlin:0.4.7'
        testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-javax-validation:0.4.7'
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2021.0.5'
        }
    }

    test {
        useJUnitPlatform()
    }
}

bootJar {
    enabled = false
}
